#!/usr/bin/awk -f
#
# BonesRedoDeathTracker.awk
# Author: MCLV-pandorabox
# License: MIT No Attribution (MIT-0)
#
# Parses debug.txt from a Minetest server running the
# [Bones Redo mod by OgelGames](https://github.com/OgelGames/bones).
# Tracks bones placement, movement, and removal, outputting a summary of bones locations by player.
#
# This script is ONLY for logs generated by the Bones Redo mod, not the original Minetest bones.
#
# Usage:
#   awk -f BonesRedoDeathTracker.awk /path/to/debug.txt
#

function clean_loc(str) {
    gsub(/\./, "", str)
    gsub(/^\(|\)$/, "", str)
    return str
}

# Track bones when placed
/Bones placed/ {
    user = $4
    loc = clean_loc($7)
    death[user "|" loc] = 1
}

# Remove bones record when items are taken
/takes items/ {
    loc = clean_loc($10)
    for (key in death) {
        split(key, arr, "|")
        if (arr[2] == loc) delete death[key]
    }
}

# Remove bones record when bones are removed by wrench
/uses wrench:wrench/ {
    match($0, /under=([^) ]+)/, m)
    if (m[1]) {
        loc = clean_loc(m[1])
        for (key in death) {
            split(key, arr, "|")
            if (arr[2] == loc) delete death[key]
        }
    }
}

# Update bones location when moved
/Bones of .* moved from/ {
    match($0, /Bones of ([^ ]+) moved from \(([^)]+)\) to \(([^)]+)\)/, m)
    if (m[1] && m[2] && m[3]) {
        user = m[1]
        oldloc = clean_loc(m[2])
        newloc = clean_loc(m[3])
        delete death[user "|" oldloc]
        death[user "|" newloc] = 1
    }
}

END {
    for (key in death) {
        split(key, arr, "|")
        print arr[1] "\t\t" arr[2]
    }
}
